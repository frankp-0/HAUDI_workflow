# score_pgs.wdl
#
# This WDL workflow calculates a polygenic score for samples in a
# Filebacked Big Matrix (FBM) using estimated ancestry-specific
# GWAS or PGS effect sizes.
#
# Required inputs:
#   - Files associated with FBM (backing file, dimensions file, column
#   info file, samples file)
#   - A file with PGS/GWAS ancestry-specific effects
#   - A specification of ordered source and target population names
#   - An output file
#
# Output files:
#   - {out_file}: A text file with columns for sample ID ("sample") and pgs ("pgs")

version 1.0

workflow score_pgs {
  input {
    # Files associated with FBM
    File bk_file
    File info_file
    File dims_file
    File fbm_samples_file

    # Ordered specification of populations, comma-separated
    String model_populations
    String target_populations

    # Effects file generated by fit_pgs WDL
    File effects_file

    # Output file
    String out_file
  }

  call score_pgs {
    input:
      bk_file = bk_file,
      info_file = info_file,
      dims_file = dims_file,
      fbm_samples_file = fbm_samples_file,
      model_populations = model_populations,
      target_populations = target_populations,
      effects_file = effects_file,
      out_file = out_file
  }

  output {
    File pgs_file = score_pgs.pgs_file
  }
}


task score_pgs {
  input {
    File bk_file
    File info_file
    File dims_file
    File fbm_samples_file
    String model_populations
    String target_populations
    File effects_file
    String out_file
  }

  command <<<
    Rscript /scripts/score_pgs.R \
      --bk_file ~{bk_file} \
      --info_file ~{info_file} \
      --dims_file ~{dims_file} \
      --fbm_samples_file ~{fbm_samples_file} \
      --model_populations ~{model_populations} \
      --target_populations ~{target_populations} \
      --effects_file ~{effects_file} \
      --out_file ~{out_file}
    >>>

  output {
    File pgs_file = "${out_file}"
  }

  runtime {
    docker: "frankpo/run_haudi:0.0.12"
  }
}
